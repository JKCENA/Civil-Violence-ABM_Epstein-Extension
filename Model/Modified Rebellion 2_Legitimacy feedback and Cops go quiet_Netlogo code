breed [ agents an-agent ]
breed [ cops cop ]

globals [
  k                   ; factor for determining arrest probability
  threshold           ; by how much must G > N to make someone rebel?
]

cops-own [
  risk-aversion*      ; R*, fixed for the cop's lifetime
  perceived-hardship*          ; H*, grievance from participating in the regime
  incentive*          ; I*, incentive for the cop to arrest
  net-risk*           ; N*, net risk for the cop
  probability-overwhelmed* ; P*, probability of provoking agents
  quiet?
]

agents-own [
  risk-aversion       ; R, fixed for the agent's lifetime, ranging from 0-1 (inclusive)
  perceived-hardship  ; H, also ranging from 0-1 (inclusive)
  active?             ; if true, then the agent is actively rebelling
  jail-term           ; how many turns in jail remain? (if 0, the agent is not in jail)

]

patches-own [
  neighborhood        ; surrounding patches within the vision radius
]

to setup
  clear-all

  ; set globals
  set k 2.3
  set threshold 0.1
  set government-legitimacy 0.82
  ask patches [
    ; make background a slightly dark gray
    set pcolor gray - 1
    ; cache patch neighborhoods
    set neighborhood patches in-radius vision
  ]

  if initial-cop-density + initial-agent-density > 100 [
    user-message (word
      "The sum of INITIAL-COP-DENSITY and INITIAL-AGENT-DENSITY "
      "should not be greater than 100.")
    stop
  ]

  ; create cops
  create-cops round (initial-cop-density * .01 * count patches) [
    move-to one-of patches with [ not any? turtles-here ]
    set risk-aversion* random-float 0.5
    set perceived-hardship* random-float 0.5
    calculate-incentive-and-risk
    set quiet? false
    display-cop
  ]

  ; create agents
  create-agents round (initial-agent-density * .01 * count patches) [
    move-to one-of patches with [ not any? turtles-here ]
    set heading 0
    set risk-aversion random-float 1.0
    set perceived-hardship random-float 1.0
    set active? false
    set jail-term 0
  ]

  ; start clock and plot initial state of system
  reset-ticks
end

to go
  ask turtles [
    ; Rule M: Move to a random site within your vision
    if (breed = agents and jail-term = 0) or (breed = cops and not quiet?) [ move ]
    ; Rule A: Determine if each agent should be active or quiet
    if breed = agents and jail-term = 0 [ determine-behavior ]
    ; Rule C: Cops arrest a random active agent within their radius
    if breed = cops [ enforce ]
  ]
  ; Jailed agents get their term reduced at the end of each clock tick
  ask agents [ if jail-term > 0 [ set jail-term jail-term - 1 ] ]

  ; Update the government legitimacy dynamically
  update-legitimacy

  ; update agent display
  ask agents [ display-agent ]
  ask cops [ display-cop ]
  ; advance clock and update plots
  tick
end

; COPS BEHAVIOR AND INCENTIVES
to calculate-incentive-and-risk
  ; I* = Legitimacy * (1 - H*)
  set incentive* government-legitimacy * (1 - perceived-hardship*)

  ; Calculate P* using dynamic formula
  let a_c count agents-on neighborhood
  let c_c 1 + count (cops-on neighborhood) with [ incentive* - net-risk* > threshold ]
  set probability-overwhelmed* 1 - exp (- k * floor (a_c / c_c))

  ; N* = R* * P*
  set net-risk* risk-aversion* * probability-overwhelmed*
end


to enforce
  calculate-incentive-and-risk

  ifelse incentive* - net-risk* > threshold [
    set quiet? false
    if any? (agents-on neighborhood) with [ active? ] [
      ; arrest suspect
      let suspect one-of (agents-on neighborhood) with [ active? ]
      move-to suspect  ; move to patch of the jailed agent
      ask suspect [
        set active? false
        set jail-term random max-jail-term
      ]
    ]
  ]
  [
    ; do nothing or stop enforcement if incentive is not greater than risk
    set quiet? true
  ]
end


; LEGITIMACY FEEDBACK
to update-legitimacy
  let jailed-count count agents with [jail-term > 0]
  let rebellious-count count agents with [ active? ]
  if count agents > 0 [
  set government-legitimacy max (list 0 (government-legitimacy - 0.001 * ((jailed-count / count agents) + (rebellious-count / count agents))))
]
end

; AGENT AND COP MOVEMENT
to move ; turtle procedure
  if movement? or (breed = cops and not quiet?) [
    ; move to a patch in vision; candidate patches are
    ; empty or contain only jailed agents
    let targets neighborhood with [
      not any? cops-here and all? agents-here [ jail-term > 0 ]
    ]
    if any? targets [ move-to one-of targets ]
  ]
end

; AGENT BEHAVIOR
to determine-behavior
  set active? (grievance - risk-aversion * estimated-arrest-probability > threshold)
end

to-report grievance
  report perceived-hardship * (1 - government-legitimacy)
end

to-report estimated-arrest-probability
  let c count (cops-on neighborhood) with [not quiet?]
  let a 1 + count (agents-on neighborhood) with [ active? ]
  ; See Info tab for a discussion of the following formula
  report 1 - exp (- k * floor (c / a))
end

; VISUALIZATION OF AGENTS AND COPS
to display-agent  ; agent procedure
  ifelse visualization = "2D"
    [ display-agent-2d ]
    [ display-agent-3d ]
end

to display-agent-2d  ; agent procedure
  set shape "circle"
  ifelse active?
    [ set color red ]
    [ ifelse jail-term > 0
        [ set color black + 3 ]
        [ set color scale-color green grievance 1.5 -0.5 ] ]
end

to display-agent-3d  ; agent procedure
  set color scale-color green grievance 1.5 -0.5
  ifelse active?
    [ set shape "person active" ]
    [ ifelse jail-term > 0
        [ set shape "person jailed" ]
        [ set shape "person quiet" ] ]
end

to display-cop
  ifelse quiet? [
    set color brown
    ask patch-here [set pcolor yellow]
  ] [
  set color cyan
    ask patch-here [set pcolor gray - 1]
  ]

  ifelse visualization = "2D"
    [ set shape "triangle" ]
    [ set shape "person soldier" ]
end
